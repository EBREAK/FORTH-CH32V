
\ SPI NOR FLASH PIN CONNECT:
\ YDCH32V307VCT6 BOARD
\ [               ] CS   <----- CS   PA2  [          ]
\ [ SPI NOR FLASH ] MOSI <----- MOSI PA7  [ CH32V307 ]
\ [               ] MISO -----> MISO PA6  [          ]
\ [               ] CLK  <----- CLK  PA5  [          ]

: SPI1-CLOCK-INIT

\ DEASSERT SPI1 RESET
RCC_BASE R32_RCC_APB2PRSTR + DUP L@ RCC_SPI1 INVERT AND SWAP L!

\ ENABLE SPI1 CLOCK
RCC_BASE R32_RCC_APB2PCENR + DUP L@ RCC_SPI1 OR SWAP L!

;

: SPI1-GPIO-INIT
\ CONFIG GPIO MDOE
$1 $2 LSHIFT GPIOA_BASE R32_GPIO_BSHR + L!
GPIOA_BASE R32_GPIO_CFGLR + DUP L@
GPIO_MODE_MASK $08 LSHIFT    \ PA2
GPIO_MODE_MASK $14 LSHIFT OR \ PA5
GPIO_MODE_MASK $18 LSHIFT OR \ PA6
GPIO_MODE_MASK $1C LSHIFT OR \ PA7
INVERT AND
GPIO_MODE_PPOUT_50M $08 LSHIFT OR \ PA2
GPIO_MODE_MUPPOUT_50M $14 LSHIFT OR \ PA5
GPIO_MODE_FLOATIN $18 LSHIFT OR \ PA6
GPIO_MODE_MUPPOUT_50M $1C LSHIFT OR \ PA7
SWAP L!
;

\ INIT SPI1
: SPI1-MASTER-INIT
$0 
SPI1_BASE R16_SPI_CTLR1 + W!
$0 
SPI1_BASE R16_SPI_CTLR2 + W!
SPI_DFF8  SPI_SSM_SW OR SPI_SSI_HIGH OR
SPI_BR_DIV8 OR SPI_MSTR_MASTER OR \ 96MHZ / 8 = 12MHZ
SPI1_BASE R16_SPI_CTLR1 + W!
SPI_I2SMOD_SPI
SPI1_BASE R16_SPI_I2S_CFGR + W!
SPI1_BASE R16_SPI_CTLR1 + DUP W@ SPI_SPE OR SWAP W!
;

SPI1-CLOCK-INIT SPI1-GPIO-INIT SPI1-MASTER-INIT

: CS-HIGH $1 $2 LSHIFT GPIOA_BASE R32_GPIO_BSHR + L! ;
: CS-LOW  $1 $12 LSHIFT GPIOA_BASE R32_GPIO_BSHR + L! ;

: SPI1-STATR@ SPI1_BASE R16_SPI_STATR + W@ ;
: SPI1-TXE? SPI1-STATR@ SPI_TXE AND 0<> ;
: SPI1-RXNE? SPI1-STATR@ SPI_RXNE AND 0<> ; 

: SPI1-WAIT-TX BEGIN PAUSE SPI1-TXE? UNTIL ;
: SPI1-WAIT-RX BEGIN PAUSE SPI1-RXNE? UNTIL ;

: SPI1-DATAR! ( V -- ) SPI1_BASE R16_SPI_DATAR + W! ;
: SPI1-DATAR@ ( V -- ) SPI1_BASE R16_SPI_DATAR + W@ ;

: SPI1-TRANSC ( TXC -- RXC )
SPI1-WAIT-TX SPI1-DATAR! SPI1-WAIT-RX SPI1-DATAR@ ;

\ BOARD YDCH32V307VCT6 : $00EF4016 (W25Q32)
: SPI-NOR-FLASH-JEDEC-ID ( -- ID )
CS-LOW
$9F SPI1-TRANSC DROP
$00 SPI1-TRANSC $10 LSHIFT
$00 SPI1-TRANSC $8 LSHIFT OR
$00 SPI1-TRANSC OR
CS-HIGH ;

: SPI-NOR-FLASH-SEND-ADDR ( ADDR -- )
DUP $10 RSHIFT SPI1-TRANSC DROP
DUP $8 RSHIFT SPI1-TRANSC DROP
SPI1-TRANSC DROP ;

: SPI-NOR-FLASH-C@ ( ADDR  -- C )
CS-LOW
$03 SPI1-TRANSC DROP
SPI-NOR-FLASH-SEND-ADDR
$00 SPI1-TRANSC
CS-HIGH ;

: SPI-NOR-FLASH-CDUMP ( ADDR COUNT -- )
CS-LOW
$03 SPI1-TRANSC DROP
SWAP SPI-NOR-FLASH-SEND-ADDR
DUP IF BEGIN $00 SPI1-TRANSC $2 NHEX. SPACE 1- DUP 0= UNTIL THEN DROP 
CS-HIGH ;

: SPI-NOR-FLASH-TYPE ( ADDR COUNT -- )
CS-LOW
$03 SPI1-TRANSC DROP
SWAP SPI-NOR-FLASH-SEND-ADDR
DUP IF BEGIN $00 SPI1-TRANSC EMIT 1- DUP 0= UNTIL THEN DROP 
CS-HIGH ;

: SPI-NOR-FLASH-WREN ( -- )
CS-LOW
$06 SPI1-TRANSC DROP
CS-HIGH ;

: SPI-NOR-FLASH-WRDIS ( -- )
CS-LOW
$04 SPI1-TRANSC DROP
CS-HIGH ;

: SPI-NOR-FLASH-BUSY? ( -- FLAG )
CS-LOW
$05 SPI1-TRANSC DROP
$FF SPI1-TRANSC $1 AND 0<>
CS-HIGH ;

: SPI-NOR-FLASH-WAIT ( -- )
BEGIN PAUSE SPI-NOR-FLASH-BUSY? INVERT UNTIL ;

: SPI-NOR-FLASH-ERASE-4K ( ADDR -- )
SPI-NOR-FLASH-WAIT
SPI-NOR-FLASH-WREN
CS-LOW
$20 SPI1-TRANSC DROP
SPI-NOR-FLASH-SEND-ADDR
CS-HIGH
SPI-NOR-FLASH-WRDIS
SPI-NOR-FLASH-WAIT ;

: SPI-NOR-FLASH-C! ( C ADDR -- )
SPI-NOR-FLASH-WAIT
SPI-NOR-FLASH-WREN
CS-LOW
$02 SPI1-TRANSC DROP
SPI-NOR-FLASH-SEND-ADDR
SPI1-TRANSC DROP
CS-HIGH
SPI-NOR-FLASH-WRDIS
SPI-NOR-FLASH-WAIT ;

