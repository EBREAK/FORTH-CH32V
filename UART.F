
\ UART PIN CONNECT:
\ YDCH32V307VCT6 BOARD
\ [USB2UART] RX <------- TX PB10 [CH32V307]
\ [USB2UART] TX -------> RX PB11 [CH32V307]

: UART3-CLOCK-INIT

\ DEASSERT GPIOB RESET
RCC_BASE R32_RCC_APB2PRSTR + DUP L@ RCC_IOPB INVERT AND SWAP L!
\ DEASSERT USART3 RESET
RCC_BASE R32_RCC_APB1PRSTR + DUP L@ RCC_USART3 INVERT AND SWAP L!

\ ENABLE GPIOB CLOCK
RCC_BASE R32_RCC_APB2PCENR + DUP L@ RCC_IOPB OR SWAP L!
\ ENABLE USART3 CLOCK
RCC_BASE R32_RCC_APB1PCENR + DUP L@ RCC_USART3 OR SWAP L!

;

: UART3-GPIO-INIT
GPIOB_BASE R32_GPIO_CFGHR + DUP L@
GPIO_MODE_MASK $8 LSHIFT 
GPIO_MODE_MASK $C LSHIFT OR INVERT AND
GPIO_MODE_MUPPOUT_50M $8 LSHIFT OR
GPIO_MODE_FLOATIN $C LSHIFT OR
SWAP L!
;

: UART3-INIT
$0 UART3_BASE R32_UART_CTLR1 + L!
$0 UART3_BASE R32_UART_CTLR2 + L!
$0 UART3_BASE R32_UART_CTLR3 + L!
$0 UART3_BASE R32_UART_BRR + L!
UART_UE UART_TE OR UART_RE OR UART3_BASE R32_UART_CTLR1 + L!
$343 ( BAUD 115200 ) UART3_BASE R32_UART_BRR + L!
;

UART3-CLOCK-INIT UART3-GPIO-INIT UART3-INIT

: UART3-TC? UART3_BASE R32_UART_STATR + L@ UART_TC AND 0<> ;
: UART3-RXNE? UART3_BASE R32_UART_STATR + L@ UART_RXNE AND 0<> ;

: UART3-TX-WAIT BEGIN PAUSE UART3-TC? UNTIL ;
: UART3-RX-WAIT BEGIN PAUSE UART3-RXNE? UNTIL ;

: UART3-EMIT ( C -- ) UART3-TX-WAIT UART3_BASE R32_UART_DATAR + L! ;
: UART3-KEY ( C -- ) UART3-RX-WAIT UART3_BASE R32_UART_DATAR + L@ ;

: UART3-ECHO-TEST ( -- ) BEGIN UART3-KEY UART3-EMIT AGAIN ;

