	DFW WIB, "WIB"
	PPUSH a5
	lw a5, USER_WIB(tp)
	ret
	ENDW WIB

	DFW WIB_NUM_USED, "WIB-NUM-USED"
	PPUSH a5
	lw a5, USER_WIN(tp)
	li a0, WORD_NLEN_MAX
	rem a5, a5, a0
	ret
	ENDW WIB_NUM_USED

	DFW WIB_RST, "WIB-RST"
	sb zero, USER_WIN(tp)
	ret
	ENDW WIB_RST

	DFW WIB_SIZE, "WIB-SIZE"
	PPUSH a5
	li a5, WORD_NLEN_MAX
	ret
	ENDW WIB_SIZE

	DFW WIB_PUSH, "WIB-PUSH"
	lw a0, USER_WIB(tp)
	lw a1, USER_WIN(tp)
	li a2, WORD_NLEN_MAX
	rem a1, a1, a2
	add a3, a0, a1
	sb a5, 0(a3)
	PPOP a5
	addi a1, a1, 1
	rem a1, a1, a2
	sw a1, USER_WIN(tp)
	ret
	ENDW WIB_PUSH

	DFW ISDELIM, "DELIM?"
	mv a0, a5
	li a5, FORTH_TRUE
	li a1, ' '
	beq a0, a1, 1f
	li a1, '\n'
	beq a0, a1, 1f
	li a1, '\r'
	beq a0, a1, 1f
	li a5, FORTH_FALSE
1:
	ret
	ENDW ISDELIM

	DFW WORD_GET, "WORD-GET"
	RPUSH ra
	FC W_WIB_RST
1:
	FC W_KEY; FC W_DUP; FC W_ISDELIM; FC W_0BRANCH; .2BYTE 2f - .;
	FC W_DROP; FC W_WIB_NUM_USED; FC W_NEZ; FC W_0BRANCH; .2BYTE 1b - .;
	RPOP ra
	ret
2:
	FC W_WIB_PUSH; j 1b
	ENDW WORD_GET

	DFW LINKLOAD, "LINK@"
	addi a5, a5, -4
	lhu a0, 0(a5)
	lhu a1, 2(a5)
	slli a1, a1, 16
	or a5, a0, a1
	andi a5, a5, -2
	ret
	ENDW LINKLOAD

	DFW NLENLOAD, "NLEN@"
	addi a5, a5, -6
	lbu a5, 0(a5)
	ret
	ENDW NLENLOAD

	DFW XT2NAME, "XT>NAME"
	addi a0, a5, -6
	lbu a1, 0(a0)
	addi a1, a1, 1
	andi a1, a1, -2
	sub a5, a0, a1
	ret
	ENDW XT2NAME

	DFW WORDLIST_WORDS, "WORDLIST-WORDS"
	RPUSH ra
	FC W_DUP; FC W_0BRANCH; .2BYTE 2f - .
1:
	FC W_DUP; FC W_XT2NAME; FC W_OVER; FC W_NLENLOAD;
	FC W_TYPE; FC W_SPACE;
	FC W_LINKLOAD; FC W_DUP; FC W_EQZ; FC W_0BRANCH; .2BYTE 1b - .
2:
	FC W_DROP;
	RPOP ra
	ret
	ENDW WORDLIST_WORDS

	DFW WORDLIST_SEARCH, "WORDLIST-SEARCH"
	# ( C-ADDR COUNT WID -- 0 | XT )
	RPUSH ra
	FC W_DUP; FC W_0BRANCH; .2BYTE 3f - .
	FC W_OVER; FC W_0BRANCH; .2BYTE 3f - .
1:
	FC W_DUP; FC W_XT2NAME; FC W_OVER; FC W_NLENLOAD; 
	FC W_TOR; FC W_2OVER; FC W_ROT; FC W_FROMR; FC W_COMPARE;
	FC W_EQZ; FC W_0BRANCH; .2BYTE 2f - .
	FC W_NIP; FC W_NIP;
	j 4f
2:
	FC W_LINKLOAD; FC W_DUP; FC W_EQZ; FC W_0BRANCH; .2BYTE 1b - .;
3:
	FC W_3DROP; FC W_0X0;
4:
	RPOP ra
	ret
	ENDW WORDLIST_SEARCH

	DFW PSP_RST, "PSP-RST"
	lw s1, USER_PS0(tp)
	li a5, TOS_INIT
	ret
	ENDW PSP_RST

	DFW PSP_CHK, "PSP-CHK"
	lw a0, USER_PS0(tp)
	li a1, FORTH_FALSE
	bgt s1, a0, 1f
	li a1, FORTH_TRUE
1:
	PPUSH a5
	mv a5, a1
	ret
	ENDW PSP_CHK

	DFW STATE, "STATE"
	PPUSH a5
	addi a5, tp, USER_STATE
	ret
	ENDW STATE

	DFW ISIMMEDIATE, "IMMEDIATE?"
	addi a5, a5, -4
	lbu a0, 0(a5)
	andi a0, a0, 1
	li a5, FORTH_FALSE
	beqz a0, 1f
	li a5, FORTH_TRUE
1:
	ret
	ENDW ISIMMEDIATE

	.section .data
	.p2align 2, 0
FORTH_CONTEXT:
	.fill CONTEXT_MAX, 4, 0

	DFW CONTEXT, "CONTEXT"
	PPUSH a5
	la a5, FORTH_CONTEXT
	ret
	ENDW CONTEXT

	DFW CONTEXT_MAX, "CONTEXT-MAX"
	PPUSH a5
	li a5, CONTEXT_MAX
	ret
	ENDW CONTEXT_MAX

	DFW WORDS, "WORDS"
	RPUSH ra
	FC W_CONTEXT; FC W_CONTEXT_MAX;
1:
	FC W_OVER; FC W_LOAD; FC W_DUP; FC W_0BRANCH; .2BYTE 2f - .
	FC W_DUP; FC W_LOAD; FC W_WORDLIST_WORDS; FC W_CR;
2:
	FC W_DROP; FC W_SWAP; FC W_4PLUS; FC W_SWAP; FC W_1MINUS;
	FC W_DUP; FC W_EQZ; FC W_0BRANCH; .2BYTE 1b - .
	FC W_2DROP;
	RPOP ra
	ret
	ENDW WORDS

	DFW FIND_WORD, "FIND-WROD"
	# ( ADDR COUNT -- 0 | XT ) 
	RPUSH ra
	FC W_0X0;
1:
	FC W_3DUP; FC W_CELLS; FC W_CONTEXT; FC W_PLUS; FC W_LOAD;
	FC W_DUP; FC W_0BRANCH; .2BYTE 2f - .; FC W_LOAD;
2:
	FC W_WORDLIST_SEARCH; FC W_DUP; FC W_0BRANCH; .2BYTE 3f - .
	FC W_TOR; FC W_3DROP; FC W_FROMR; j 4f
3:
	FC W_DROP; FC W_1PLUS; FC W_DUP; FC W_CONTEXT_MAX; FC W_EQ;
	FC W_0BRANCH; .2BYTE 1b - .; FC W_3DROP; FC W_0X0;
4:
	RPOP ra
	ret
	ENDW FIND_WORD

	IDFW TICK, "'"
	RPUSH ra
	FC W_WORD_GET; FC W_WIB; FC W_WIB_NUM_USED; FC W_FIND_WORD
	RPOP ra
	ret
	IENDW TICK

	IDFW COMPOFF, "["
	RPUSH ra
	FC W_STATE; FC W_OFF;
	RPOP ra
	ret
	IENDW COMPOFF

	DFW COMPON, "]"
	RPUSH ra
	FC W_STATE; FC W_ON;
	RPOP ra
	ret
	ENDW COMPON

	.section .rodata
MSG_NOT_FOUND:
	.ascii "NOT FOUND\r\n"
	.set LEN_MSG_NOT_FOUND, . - MSG_NOT_FOUND

	DFW INTERPRET, "INTERPRET"
	ENDW INTERPRET
	RPUSH ra
	FC W_WORD_GET; FC W_WIB; FC W_WIB_NUM_USED;
	FC W_FIND_WORD; FC W_DUP; FC W_0BRANCH; .2BYTE INTERPRET_TRY_NUMBER - .
	FC W_DUP; FC W_ISIMMEDIATE; FC W_0BRANCH; .2BYTE 1f - .
	FC W_EXECUTE; j INTERPRET_EXIT
1:
	FC W_STATE; FC W_LOAD; FC W_0BRANCH; .2BYTE 2f -.
INTERPRET_COMPILE_WORD:
	FC W_ASM_CALL; j INTERPRET_EXIT
2:
	FC W_EXECUTE; j INTERPRET_EXIT
INTERPRET_TRY_NUMBER:
	FC W_DROP; FC W_WIB; FC W_WIB_NUM_USED;
	FC W_ISNUMBER; FC W_0BRANCH; .2BYTE INTERPRET_NOT_FOUND - .
	FC W_WIB; FC W_WIB_NUM_USED; FC W_TONUMBER;
	FC W_STATE; FC W_LOAD; FC W_0BRANCH; .2BYTE 5f - .
INTERPRET_COMPILE_NUMBER:
	FC W_DUP; FC W_ABS; FC W_0X7FFF; FC W_LT; FC W_0BRANCH; .2BYTE 3f - .
	FC W_ULLIT; .4BYTE NAME_SWLIT; FC W_UWLIT; .2BYTE NLEN_SWLIT;
	FC W_FIND_WORD; FC W_ASM_CALL; FC W_WCOMMA; j 5f
3:
	FC W_DUP; FC W_ABS; FC W_0XFFFF; FC W_LT; FC W_0BRANCH; .2BYTE 4f - .
	FC W_ULLIT; .4BYTE NAME_UWLIT; FC W_UWLIT; .2BYTE NLEN_UWLIT;
	FC W_FIND_WORD; FC W_ASM_CALL; FC W_WCOMMA; j 5f
4:
	FC W_ULLIT; .4BYTE NAME_ULLIT; FC W_UWLIT; .2BYTE NLEN_ULLIT;
	FC W_FIND_WORD; FC W_ASM_CALL; FC W_LCOMMA;
	j INTERPRET_EXIT
5:
	j INTERPRET_EXIT
INTERPRET_NOT_FOUND:
	FC W_PSP_RST; FC W_STATE; FC W_OFF;
	FC W_CR; FC W_WIB; FC W_WIB_NUM_USED; FC W_TYPE; FC W_SPACE;
	FC W_ULLIT; .4BYTE MSG_NOT_FOUND; FC W_UWLIT; .2BYTE LEN_MSG_NOT_FOUND;
	FC W_TYPE; FC W_CR;
INTERPRET_EXIT:
	RPOP ra
	ret

	.section .rodata
MSG_PSERR:
	.ascii "PARAM STACK ERROR\r\n"
	.set LEN_MSG_PSERR, . - MSG_PSERR

	DFW INTERPRET_LOOP, "INTERPRET-LOOP"
1:
	FC W_INTERPRET; FC W_PSP_CHK; xori a5, a5, -1; FC W_0BRANCH; .2BYTE 2f - .
	FC W_PSP_RST; FC W_STATE; FC W_OFF;
	FC W_ULLIT; .4BYTE MSG_PSERR; FC W_UWLIT; .2BYTE LEN_MSG_PSERR; FC W_TYPE;
2:
	j 1b
	ENDW INTERPRET_LOOP
