	DFW WIB, "WIB"
	PPUSH a5
	lw a5, USER_WIB(tp)
	ret
	ENDW WIB

	DFW WIB_NUM_USED, "WIB-NUM-USED"
	PPUSH a5
	lw a5, USER_WIN(tp)
	li a0, WORD_NLEN_MAX
	rem a5, a5, a0
	ret
	ENDW WIB_NUM_USED

	DFW WIB_RST, "WIB-RST"
	sb zero, USER_WIN(tp)
	ret
	ENDW WIB_RST

	DFW WIB_SIZE, "WIB-SIZE"
	PPUSH a5
	li a5, WORD_NLEN_MAX
	ret
	ENDW WIB_SIZE

	DFW WIB_PUSH, "WIB-PUSH"
	lw a0, USER_WIB(tp)
	lw a1, USER_WIN(tp)
	li a2, WORD_NLEN_MAX
	rem a1, a1, a2
	add a3, a0, a1
	sb a5, 0(a3)
	PPOP a5
	addi a1, a1, 1
	rem a1, a1, a2
	sw a1, USER_WIN(tp)
	ret
	ENDW WIB_PUSH

	DFW ISDELIM, "DELIM?"
	mv a0, a5
	li a5, FORTH_TRUE
	li a1, ' '
	beq a0, a1, 1f
	li a1, '\n'
	beq a0, a1, 1f
	li a1, '\r'
	beq a0, a1, 1f
	li a5, FORTH_FALSE
1:
	ret
	ENDW ISDELIM

	DFW WORD_GET, "WORD-GET"
	RPUSH ra
	NC W_WIB_RST
1:
	NC W_KEY; NC W_DUP; NC W_ISDELIM; NC W_0BRANCH; .2BYTE 2f - .;
	NC W_DROP; NC W_WIB_NUM_USED; NC W_NEZ; NC W_0BRANCH; .2BYTE 1b - .;
	RPOP ra
	ret
2:
	NC W_WIB_PUSH; j 1b
	ENDW WORD_GET

	DFW LINKLOAD, "LINK@"
	addi a5, a5, -4
	lhu a0, 0(a5)
	lhu a1, 2(a5)
	slli a1, a1, 16
	or a5, a0, a1
	andi a5, a5, -2
	ret
	ENDW LINKLOAD

	DFW NLENLOAD, "NLEN@"
	addi a5, a5, -6
	lbu a5, 0(a5)
	ret
	ENDW NLENLOAD

	DFW XT2NAME, "XT>NAME"
	addi a0, a5, -6
	lbu a1, 0(a0)
	addi a1, a1, 1
	andi a1, a1, -2
	sub a5, a0, a1
	ret
	ENDW XT2NAME

	DFW WORDLIST_WORDS, "WORDLIST-WORDS"
	RPUSH ra
	NC W_DUP; NC W_0BRANCH; .2BYTE 2f - .
1:
	NC W_DUP; NC W_XT2NAME; NC W_OVER; NC W_NLENLOAD;
	NC W_TYPE; NC W_SPACE;
	NC W_LINKLOAD; NC W_DUP; NC W_EQZ; NC W_0BRANCH; .2BYTE 1b - .
2:
	NC W_DROP;
	RPOP ra
	ret
	ENDW WORDLIST_WORDS

	DFW WORDLIST_SEARCH, "WORDLIST-SEARCH"
	# ( C-ADDR COUNT WID -- 0 | XT )
	RPUSH ra
	NC W_DUP; NC W_0BRANCH; .2BYTE 3f - .
	NC W_OVER; NC W_0BRANCH; .2BYTE 3f - .
1:
	NC W_DUP; NC W_XT2NAME; NC W_OVER; NC W_NLENLOAD; 
	NC W_TOR; NC W_2OVER; NC W_ROT; NC W_FROMR; NC W_COMPARE;
	NC W_EQZ; NC W_0BRANCH; .2BYTE 2f - .
	NC W_NIP; NC W_NIP;
	j 4f
2:
	NC W_LINKLOAD; NC W_DUP; NC W_EQZ; NC W_0BRANCH; .2BYTE 1b - .;
3:
	NC W_3DROP; NC W_0X0;
4:
	RPOP ra
	ret
	ENDW WORDLIST_SEARCH

	DFW PSP_RST, "PSP-RST"
	lw s1, USER_PS0(tp)
	li a5, TOS_INIT
	ret
	ENDW PSP_RST

	DFW PSP_CHK, "PSP-CHK"
	lw a0, USER_PS0(tp)
	li a1, FORTH_FALSE
	bgt s1, a0, 1f
	li a1, FORTH_TRUE
1:
	PPUSH a5
	mv a5, a1
	ret
	ENDW PSP_CHK

	DFW STATE, "STATE"
	PPUSH a5
	addi a5, tp, USER_STATE
	ret
	ENDW STATE

	DFW ISIMMEDIATE, "IMMEDIATE?"
	addi a5, a5, -4
	lbu a0, 0(a5)
	andi a0, a0, 1
	li a5, FORTH_FALSE
	beqz a0, 1f
	li a5, FORTH_TRUE
1:
	ret
	ENDW ISIMMEDIATE

	.section .data, "a"
	.p2align 2, 0
FORTH_CONTEXT:
	.fill CONTEXT_MAX, 4, 0

	DFW CONTEXT, "CONTEXT"
	PPUSH a5
	la a5, FORTH_CONTEXT
	ret
	ENDW CONTEXT

	DFW CONTEXT_MAX, "CONTEXT-MAX"
	PPUSH a5
	li a5, CONTEXT_MAX
	ret
	ENDW CONTEXT_MAX

	DFW WORDS, "WORDS"
	RPUSH ra
	NC W_CONTEXT; NC W_CONTEXT_MAX;
1:
	NC W_OVER; NC W_LOAD; NC W_DUP; NC W_0BRANCH; .2BYTE 2f - .
	NC W_DUP; NC W_LOAD; NC W_WORDLIST_WORDS; NC W_CR;
2:
	NC W_DROP; NC W_SWAP; NC W_4PLUS; NC W_SWAP; NC W_1MINUS;
	NC W_DUP; NC W_EQZ; NC W_0BRANCH; .2BYTE 1b - .
	NC W_2DROP;
	RPOP ra
	ret
	ENDW WORDS

	DFW FIND_WORD, "FIND-WROD"
	# ( ADDR COUNT -- 0 | XT ) 
	RPUSH ra
	NC W_0X0;
1:
	NC W_3DUP; NC W_CELLS; NC W_CONTEXT; NC W_PLUS; NC W_LOAD;
	NC W_DUP; NC W_0BRANCH; .2BYTE 2f - .; NC W_LOAD;
2:
	NC W_WORDLIST_SEARCH; NC W_DUP; NC W_0BRANCH; .2BYTE 3f - .
	NC W_TOR; NC W_3DROP; NC W_FROMR; j 4f
3:
	NC W_DROP; NC W_1PLUS; NC W_DUP; NC W_CONTEXT_MAX; NC W_EQ;
	NC W_0BRANCH; .2BYTE 1b - .; NC W_3DROP; NC W_0X0;
4:
	RPOP ra
	ret
	ENDW FIND_WORD

	IDFW TICK, "'"
	RPUSH ra
	NC W_WORD_GET; NC W_WIB; NC W_WIB_NUM_USED; NC W_FIND_WORD
	RPOP ra
	ret
	IENDW TICK

	IDFW COMPOFF, "["
	RPUSH ra
	NC W_STATE; NC W_OFF;
	RPOP ra
	ret
	IENDW COMPOFF

	DFW COMPON, "]"
	RPUSH ra
	NC W_STATE; NC W_ON;
	RPOP ra
	ret
	ENDW COMPON

	.section .data, "a"
	.p2align 2, 0
RUNTIME_LATEST:
	.4BYTE 0x0

	DFW RUNTIME, "RUNTIME"
	PPUSH a5
	la a5, RUNTIME_LATEST
	ret
	ENDW RUNTIME

	DFW WORD_NEW, "WORD-NEW"
	# ( ADDR COUNT -- XT )
	RPUSH ra
	NC W_WALIGN; NC W_2DUP; NC W_HERE; NC W_SWAP; NC W_CMOVE;
	NC W_NIP; NC W_DUP; NC W_WALIGNED; NC W_ALLOT;
	NC W_WALIGN; NC W_WCOMMA;
	NC W_CONTEXT; NC W_LOAD; NC W_LOAD; NC W_LCOMMA;
	NC W_HERE;
	RPOP ra
	ret
	ENDW WORD_NEW

	DFW BLENLOAD, "BLEN@"
	# ( XT -- BLEN )
	lbu a5, -5(a5)
	ret
	ENDW BLENLOAD

	DFW BLENSTORE, "BLEN!"
	# ( BLEN XT -- )
	lw a1, 0(s1)
	sb a1, -5(a5)
	lw a5, 4(s1)
	addi s1, s1, 8
	ret
	ENDW BLENSTORE

	DFW WORD_END, "WORD-END"
	# ( XT -- )
	RPUSH ra
	NC W_HERE; NC W_OVER; NC W_MINUS; NC W_OVER; NC W_BLENSTORE;
	NC W_CONTEXT; NC W_LOAD; NC W_STORE;
	RPOP ra
	ret
	ENDW WORD_END

	DFW COLON, ":"
	# ( -- XT )
	RPUSH ra
	NC W_WORD_GET; NC W_WIB; NC W_WIB_NUM_USED;
	NC W_WORD_NEW; NC W_COMPON;
	NC W_0X1; NC W_ASM_RPUSH;
	RPOP ra
	ret
	ENDW COLON

	IDFW SEMICOLON, ";"
	# ( XT -- )
	RPUSH ra
	NC W_0X1; NC W_ASM_RPOP; NC W_ASM_RET;
	NC W_WORD_END; NC W_COMPOFF;
	RPOP ra
	ret
	IENDW SEMICOLON

	DFW CODECOLON, ":CODE"
	# ( -- XT )
	RPUSH ra
	NC W_WORD_GET; NC W_WIB; NC W_WIB_NUM_USED;
	NC W_WORD_NEW;
	RPOP ra
	ret
	ENDW CODECOLON

	DFW CODESEMICOLON, "CODE;"
	# ( XT -- )
	RPUSH ra
	NC W_ASM_RET; NC W_WORD_END;
	RPOP ra
	ret
	ENDW CODESEMICOLON

	IDFW BEGIN, "BEGIN"
	j W_HERE
	IENDW BEGIN

	IDFW AGAIN, "AGAIN"
	RPUSH ra
	NC W_HERE; NC W_MINUS;
	NC W_0X0; NC W_ASM_JAL;
	RPOP ra
	ret
	IENDW AGAIN

	IDFW UNTIL, "UNTIL"
	RPUSH ra
	NC W_ULLIT; .4BYTE NAME_0BRANCH; NC W_UWLIT; .2BYTE NLEN_0BRANCH;
	NC W_FIND_WORD; NC W_ASM_CALL;
	NC W_HERE; NC W_MINUS; NC W_WCOMMA;
	RPOP ra
	ret
	IENDW UNTIL

	IDFW IF, "IF"
	RPUSH ra
	NC W_ULLIT; .4BYTE NAME_0BRANCH; NC W_UWLIT; .2BYTE NLEN_0BRANCH;
	NC W_FIND_WORD; NC W_ASM_CALL;
	NC W_HERE; NC W_0X0; NC W_WCOMMA;
	RPOP ra
	ret
	IENDW IF

	IDFW THEN, "THEN"
	RPUSH ra
	NC W_HERE; NC W_OVER; NC W_MINUS; NC W_SWAP; NC W_WSTORE;
	RPOP ra
	ret
	IENDW THEN

	.section .rodata
MSG_NOT_FOUND:
	.ascii "NOT FOUND\r\n"
	.set LEN_MSG_NOT_FOUND, . - MSG_NOT_FOUND

	DFW NUMBER_COMPILE, "NUMBER-COMPILE"
	# ( NUM -- )
	RPUSH ra
	NC W_DUP; NC W_ABS; NC W_0X7FFF; NC W_LT; NC W_0BRANCH; .2BYTE 1f - .
	NC W_ULLIT; .4BYTE NAME_SWLIT; NC W_UWLIT; .2BYTE NLEN_SWLIT;
	NC W_FIND_WORD; NC W_ASM_CALL; NC W_WCOMMA; j 3f
1:
	NC W_DUP; NC W_ABS; NC W_0XFFFF; NC W_LT; NC W_0BRANCH; .2BYTE 2f - .
	NC W_ULLIT; .4BYTE NAME_UWLIT; NC W_UWLIT; .2BYTE NLEN_UWLIT;
	NC W_FIND_WORD; NC W_ASM_CALL; NC W_WCOMMA; j 3f
2:
	NC W_ULLIT; .4BYTE NAME_ULLIT; NC W_UWLIT; .2BYTE NLEN_ULLIT;
	NC W_FIND_WORD; NC W_ASM_CALL; NC W_LCOMMA;
3:
	RPOP ra
	ret
	ENDW NUMBER_COMPILE

	DFW INTERPRET_NOT_FOUND, "INTERPRET-NOT-FOUND"
	RPUSH ra
	NC W_PSP_RST; NC W_STATE; NC W_OFF;
	NC W_CR; NC W_WIB; NC W_WIB_NUM_USED; NC W_TYPE; NC W_SPACE;
	NC W_ULLIT; .4BYTE MSG_NOT_FOUND; NC W_UWLIT; .2BYTE LEN_MSG_NOT_FOUND;
	NC W_TYPE; NC W_CR;
	RPOP ra
	ret
	ENDW INTERPRET_NOT_FOUND

	DFW INTERPRET, "INTERPRET"
	RPUSH ra
	NC W_WORD_GET; NC W_WIB; NC W_WIB_NUM_USED;
	NC W_FIND_WORD; NC W_DUP; NC W_0BRANCH; .2BYTE INTERPRET_TRY_NUMBER - .
	NC W_DUP; NC W_ISIMMEDIATE; NC W_0BRANCH; .2BYTE 1f - .
	# sucks goto, because this word body is > 256BYTE, we need make it smaller
	j 2f
1:
	NC W_STATE; NC W_LOAD; NC W_0BRANCH; .2BYTE 2f -.
INTERPRET_COMPILE_WORD:
	NC W_ASM_CALL; j INTERPRET_EXIT
2:
	NC W_EXECUTE; j INTERPRET_EXIT
INTERPRET_TRY_NUMBER:
	NC W_DROP; NC W_WIB; NC W_WIB_NUM_USED;
	NC W_ISNUMBER; NC W_0BRANCH; .2BYTE INTERPRET_NOT_FOUND - .
	NC W_WIB; NC W_WIB_NUM_USED; NC W_TONUMBER;
	NC W_STATE; NC W_LOAD; NC W_0BRANCH; .2BYTE 3f - .
	NC W_NUMBER_COMPILE
3:
	j INTERPRET_EXIT
INTERPRET_NOT_FOUND:
	NC W_INTERPRET_NOT_FOUND
INTERPRET_EXIT:
	RPOP ra
	ret
	ENDW INTERPRET

	.section .rodata
MSG_PSERR:
	.ascii "PARAM STACK ERROR\r\n"
	.set LEN_MSG_PSERR, . - MSG_PSERR

	DFW INTERPRET_LOOP, "INTERPRET-LOOP"
1:
	NC W_INTERPRET; NC W_PSP_CHK; xori a5, a5, -1; NC W_0BRANCH; .2BYTE 2f - .
	NC W_PSP_RST; NC W_STATE; NC W_OFF;
	NC W_ULLIT; .4BYTE MSG_PSERR; NC W_UWLIT; .2BYTE LEN_MSG_PSERR; NC W_TYPE;
2:
	j 1b
	ENDW INTERPRET_LOOP
